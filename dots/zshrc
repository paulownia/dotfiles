# vim: set ft=zsh :

# zshrc is loaded in interactive mode.

# basic aliases
alias here="open ."
alias home="open ~/"
alias edit="open -a CotEditor"
alias gmail="open -na 'Google Chrome' --args --app='https://mail.google.com/mail/u/0/#inbox'"
alias bash="HISTFILE=${HOME}/.bash_history /bin/bash"
alias reload="exec ${SHELL}"
alias preview="open -a 'Preview'"
alias acl="/bin/ls -lhGe"

# prompt setting
case "${OSTYPE}" in
	darwin*)
		if [[ ${LC_TERMINAL} == iTerm2 ]] || [[ ${TERM_PROGRAM} == vscode ]] || [[ ${TERMINAL_EMULATOR} == *JediTerm ]]; then
			X_PROMPT="%B%F{cyan}%3~%f%b"
		else
			X_PROMPT="%F{green}${USER}@${HOST%%.*}%f%b:%F{blue}%3~%f%b"
		fi
		;;
	linux*)
		X_PROMPT="%F{magenta}${USER}@${HOST%%.*}%f%b:%F{blue}%1~%f%b"
		;;
esac

function zle-line-init zle-keymap-select {
	case $KEYMAP in
		main|viins)
			PROMPT="${X_PROMPT} %F{cyan}i%f%b%(!.#.$) "
			;;
		vicmd)
			PROMPT="${X_PROMPT} %F{red}n%f%b%(!.#.$) "
			;;
		vivis|vivli)
			PROMPT="${X_PROMPT} %F{red}v%f%b%(!.#.$) "
			;;
	esac
	zle reset-prompt
}
zle -N zle-line-init
zle -N zle-keymap-select

# history
HISTFILE=${HOME}/.zsh_history
HISTSIZE=100000
SAVEHIST=100000
setopt HIST_IGNORE_DUPS
setopt HIST_IGNORE_SPACE
setopt EXTENDED_HISTORY
setopt SHARE_HISTORY
setopt HIST_REDUCE_BLANKS
setopt HIST_VERIFY
setopt HIST_NO_STORE
setopt noflowcontrol

# zsh binding style
bindkey -v

# zsh history searching
autoload history-search-end
zle -N history-beginning-search-backward-end history-search-end
zle -N history-beginning-search-forward-end history-search-end
bindkey '' history-beginning-search-backward-end
bindkey '' history-beginning-search-forward-end
bindkey -a '' vi-beginning-of-line
bindkey -v '' vi-beginning-of-line
bindkey -a '' vi-end-of-line
bindkey -v '' vi-end-of-line
bindkey -a '' history-incremental-search-backward
bindkey -v '' history-incremental-search-backward
bindkey -a '' history-incremental-search-forward
bindkey -v '' history-incremental-search-forward

# fix keys
bindkey '[H' beginning-of-line # home
bindkey '[F' end-of-line # end
bindkey '[5~' beginning-of-history # page up
bindkey '[6~' end-of-history # page down
bindkey -a '[2~' vi-insert # insert
bindkey -v '[2~' vi-insert # insert

# script alias
alias -s rb="ruby"
alias -s js="node"
alias -s {txt,md,html,css,scss}="edit"
alias -s {jpg,jpeg,png,gif,webp}="preview"

# beep
setopt no_hist_beep
setopt no_list_beep

# cd extension
setopt auto_cd
setopt auto_pushd
setopt pushd_ignore_dups
DIRSTACKSIZE=20

# addition custom completion to fpath
if [[ -d ~/.local/share/zsh/functions ]]; then
	fpath=(~/.local/share/zsh/functions $fpath)
fi

# addition homebrew completion to fpath
if [[ -d /opt/homebrew/share/zsh/site-functions ]]; then
	fpath=(/opt/homebrew/share/zsh/site-functions $fpath)
fi

# addition dotfiles zsh functions to fpath
if [[ -d ~/.dotfiles/zsh ]]; then
	fpath=(~/.dotfiles/zsh $fpath)
	autoload -zU ~/.dotfiles/zsh/[^_]*(:t)
fi

# zsh completion
autoload -Uz compinit
if [[ -n ~/.zcompdump(#qN.mh+24) ]]; then
	compinit
else
	compinit -C
fi

zstyle ':completion:*:default' menu selection
zstyle ':completion:*' verbose yes
zstyle ':completion:*' completer _expand _complete _match _prefix _approximate _list _history
zstyle ':completion:*:messages' format "%B%F{yellow}%d %f%b"
zstyle ':completion:*:warnings' format "%B%F{red}No matches for: %d %f%b"
zstyle ':completion:*:descriptions' format "%B%F{blue}%d %f%b"
zstyle ':completion:*:corrections' format "%B%F{yellow}%d %F{red}(errors: %e)%f%b"
zstyle ':completion:*:options' description 'yes'
zstyle ':completion:*' group-name ''


# alias another basic command
if command -v eza >/dev/null 2>&1; then
	alias ls=" eza --time-style=iso"
	alias ll=" eza -gl --time-style=long-iso"
else
	alias ls=" /bin/ls -G"
	alias ll=" /bin/ls -lFG"
fi

if command -v bat >/dev/null 2>&1; then
	alias cat="bat --theme=Dracula"
fi

# settings for java
if [[ -n $JAVA_HOME ]]; then
	alias javac="javac -J-Dfile.encoding=UTF-8"
	alias java="java -Dfile.encoding=UTF-8"
fi

# nvim
if command -v nvim >/dev/null 2>&1; then
	alias vi=nvim
	alias view="nvim -R"
	alias vimdiff="nvim -d"
fi

# git
alias git-vimdiff="git difftool --tool=vimdiff --no-prompt"

autoload -Uz vcs_info

setopt prompt_subst
zstyle ':vcs_info:git:*' check-for-changes true
zstyle ':vcs_info:git:*' stagedstr "%F{yellow}!"
zstyle ':vcs_info:git:*' unstagedstr "%F{red}+"
zstyle ':vcs_info:*' formats "%F{green}%c%u[%b]%f"
zstyle ':vcs_info:*' actionformats '[%b|%a]'
precmd () {
	vcs_info
	RPROMPT='${vcs_info_msg_0_}'
	setopt transient_rprompt
}

if [[ $LC_TERMINAL == iTerm2 ]]; then
	# change bg color on ssh login
	if command -v ssh-bg >/dev/null 2>&1; then
		alias ssh=ssh-bg
		compdef ssh-bg=ssh
	fi
fi

if [[ $OSTYPE == darwin* ]]; then
	# make command with notification
	if command -v make-notify >/dev/null 2>&1; then
		alias make=make-notify
		compdef make-notify=make
	fi
fi

# rbenv (lazy loading)
if command -v rbenv >/dev/null 2>&1; then
	rbenv() {
		unfunction rbenv
		eval "$(command rbenv init -)"
		rbenv "$@"
	}
fi

# goenv (lazy loading)
if command -v goenv >/dev/null 2>&1; then
	goenv() {
		unfunction goenv
		eval "$(command goenv init -)"
		goenv "$@"
	}
fi

# enable nvm if installed by homebrew (lazy loading with --no-use)
if [[ -n $NVM_DIR ]] && [[ -f /opt/homebrew/opt/nvm/nvm.sh ]]; then
	source "/opt/homebrew/opt/nvm/nvm.sh" --no-use
	source "/opt/homebrew/opt/nvm/etc/bash_completion.d/nvm"
	node() {
		if whence -p node &>/dev/null; then unfunction node; node "$@"
		else echo "Run 'nvm use' to activate Node.js" >&2; return 127; fi
	}
	npm() {
		if whence -p npm &>/dev/null; then unfunction npm; npm "$@"
		else echo "Run 'nvm use' to activate Node.js" >&2; return 127; fi
	}
	npx() {
		if whence -p npx &>/dev/null; then unfunction npx; npx "$@"
		else echo "Run 'nvm use' to activate Node.js" >&2; return 127; fi
	}
fi

# The next line updates PATH for the Google Cloud SDK.
if [[ -f "${HOME}/google-cloud-sdk/path.zsh.inc" ]]; then
	source "${HOME}/google-cloud-sdk/path.zsh.inc";
fi

# The next line enables shell command completion for gcloud.
if [[ -f "${HOME}/google-cloud-sdk/completion.zsh.inc" ]]; then
	source "${HOME}/google-cloud-sdk/completion.zsh.inc";
fi

# load local zshrc if exists
if [[ -f ~/.zshrc.local ]]; then
	source ~/.zshrc.local
fi

