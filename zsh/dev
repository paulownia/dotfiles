#!/bin/zsh

function _dev_add() {
	[[ -z $DEV_PROJECTS_FILE ]] && return 1

	local KEY=$1

	{
		[[ -f $DEV_PROJECTS_FILE ]] && awk -F "\t" -v key="$KEY" '$1 != key' "$DEV_PROJECTS_FILE"
		printf "%s\t%s\n" "$KEY" "$PWD"
	} | sort > "$DEV_PROJECTS_FILE.tmp"

	mv "$DEV_PROJECTS_FILE.tmp" "$DEV_PROJECTS_FILE"
}

function _dev_delete() {
	[[ -z $DEV_PROJECTS_FILE || ! -f $DEV_PROJECTS_FILE ]] && return 1
	local KEY=$1
	awk -F "\t" -v key="$KEY" '$1 != key' "$DEV_PROJECTS_FILE" > "$DEV_PROJECTS_FILE.tmp"
	mv "$DEV_PROJECTS_FILE.tmp" "$DEV_PROJECTS_FILE"
}

function _dev_cd() {
	[[ -z $DEV_PROJECTS_FILE || ! -f $DEV_PROJECTS_FILE ]] && return 1

	local KEY=$1 DIR

	if [[ -z $KEY ]]; then
		local SELECTED=$(fzf < $DEV_PROJECTS_FILE)
		[[ -z $SELECTED ]] && return 1
		KEY=${SELECTED%%$'\t'*}
		DIR=${SELECTED#*$'\t'}
	else
		DIR=$(awk -F "\t" -v "key=$KEY" '$1 == key { print $2; exit }' "$DEV_PROJECTS_FILE")
	fi

	[[ -z $DIR || ! -d $DIR ]] && return 1

	cd "$DIR" || return 1

	title $KEY

	pwd
}


function _dev_list() {
	[[ -z $DEV_PROJECTS_FILE ]] && return 1
	column -t $DEV_PROJECTS_FILE
}

while getopts d:a:sl OPT
do
	case $OPT in
		l)
			_dev_list
			return
			;;
		a)
			_dev_add "$OPTARG"
			return
			;;
		d)
			_dev_delete "$OPTARG"
			return
			;;
		*)
			return 1
	esac
done

_dev_cd $1

