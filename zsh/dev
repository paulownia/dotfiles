#autoload
emulate -L zsh
setopt extendedglob

local DEV_PROJECTS_DIR=${DEV_PROJECTS_FILE:-"$HOME/.local/share/dev-projects"}
local DEV_PROJECTS_FILE="$DEV_PROJECTS_DIR/dev"

function _dev_init() {
	if [[ ! -d $DEV_PROJECTS_DIR ]]; then
		mkdir -p $DEV_PROJECTS_DIR
	fi

	if [[ ! -f $DEV_PROJECTS_FILE ]]; then
		touch $DEV_PROJECTS_FILE
	fi
}

function _dev_add() {
	local KEY=$1

	{
		[[ -f $DEV_PROJECTS_FILE ]] && awk -F "\t" -v key="$KEY" '$1 != key' "$DEV_PROJECTS_FILE"
		printf "%s\t%s\n" "$KEY" "$PWD"
	} | sort > "$DEV_PROJECTS_FILE.tmp"

	mv "$DEV_PROJECTS_FILE.tmp" "$DEV_PROJECTS_FILE"
}

function _dev_delete() {
	local KEY=$1
	awk -F "\t" -v key="$KEY" '$1 != key' "$DEV_PROJECTS_FILE" > "$DEV_PROJECTS_FILE.tmp"
	mv "$DEV_PROJECTS_FILE.tmp" "$DEV_PROJECTS_FILE"
}

function _dev_cd() {
	local KEY=$1 DIR

	if [[ -z $KEY ]]; then
		local SELECTED=$(column -t $DEV_PROJECTS_FILE | fzf)
		[[ -z $SELECTED ]] && return 1
		KEY=${SELECTED%% *}
		DIR=${SELECTED#* }
		DIR=${DIR##[[:space:]]#}  # trim leading spaces
	else
		DIR=$(awk -F "\t" -v "key=$KEY" '$1 == key { print $2; exit }' "$DEV_PROJECTS_FILE")
	fi

	[[ -z $DIR || ! -d $DIR ]] && return 1

	cd "$DIR" || return 1

	title $KEY

	pwd
}

function _dev_list() {
	column -t $DEV_PROJECTS_FILE
}


_dev_init

while getopts d:a:sl OPT
do
	case $OPT in
		l)
			_dev_list
			return
			;;
		a)
			_dev_add "$OPTARG"
			return
			;;
		d)
			_dev_delete "$OPTARG"
			return
			;;
		*)
			return 1
	esac
done

_dev_cd $1

