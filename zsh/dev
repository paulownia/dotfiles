#!/bin/zsh

# dev command

function dev_add() {
	local KEY=$1
	local CD_PATH=$(pwd)

	if [ ! -f ~/.dev ]; then
		printf "%s\t%s" "$KEY" "$CD_PATH" > ~/.dev
	else awk -F "\t" -v "key=$KEY" '{ if ($1 != key) { print $0 } }' ~/.dev > ~/.dev_tmp
		printf "%s\t%s\n" "$KEY" "$CD_PATH" >> ~/.dev_tmp
		mv ~/.dev_tmp ~/.dev
	fi
}

function dev_delete() {
	local KEY=$1

	if [ ! -f ~/.dev ]; then
		return 1
	fi

	awk -F "\t" -v "key=$KEY" '{ if ($1 != key) { print $0 } }' ~/.dev > ~/.dev_tmp
	mv ~/.dev_tmp ~/.dev
}

function dev_cd() {
	KEY=$1
	if [ -z "$KEY" ]; then
		KEY=default
	fi

	local CD_PATH=$(awk -F "\t" -v "key=$KEY" '{ if ($1 == key) { print $2; exit } }' ~/.dev)

	if [ -z "$CD_PATH" ] || [ ! -d "$CD_PATH" ]; then
		return 1
	fi

	cd "$CD_PATH" || return 1

	title $KEY

	pwd
}

function dev_cd_select() {
	local SELECTED=$(cat ~/.dev | column -t | fzf)
	if [[ -z $SELECTED ]]; then
		return 1
	fi

	local KEY_PATH=($(echo $SELECTED | awk '{ print $1, $2 }'))
	cd $KEY_PATH[2]
	title $KEY_PATH[1]
	pwd
}

function dev_list() {
	column -t < ~/.dev
}

function dev() {
	while getopts d:a:sl OPT
	do
		case $OPT in
			l)
				dev_list
				return $!
				;;
			a)
				dev_add "$OPTARG"
				return $!
				;;
			d)
				dev_delete "$OPTARG"
				return $!
				;;
			s)
				dev_cd_select
				return $!
				;;
		esac
	done
	dev_cd $1
}

function _dev_projects() {
	local -a CD_LIST
	CD_LIST=( $(cat ~/.dev | cut -f 1) )
	_describe "Projects" CD_LIST
}

function _dev_pwd() {
	_values "keyword candidates" $(basename ${PWD})
}

function _dev() {
	_arguments \
		'(- *)'-l'[list all projects]' \
		'(- *)'-a'[add project]:keyword:_dev_pwd' \
		'(- *)'-d'[delete project]:project:_dev_projects' \
		'(- *)'-s'[select project]' \
		'(- *)*:project:_dev_projects'
}

compdef _dev dev

